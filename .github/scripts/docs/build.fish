source (status dirname)/utils.fish

function escape_path
    string replace -a / '.' $argv |
        string replace -a '@' ''
end

assert groupcmd pnpm i
assert groupcmd pnpm run build
assert groupcmd mkdir -p build/docs

echo '## Packages

[Home](./index.md)

|Package|
|---|' >build/docs/index.md

set -l dts_files (assert find ./build -name test -prune -o -name '*.d.ts')
set -l placeholder placeholder_package_name

for line in (assert node (status dirname)/parse-exports.cjs $dts_files)
    set -l map (string split \t $line)

    echo Map $map[1] to $map[2].
    assert node (status dirname)/update-config.cjs $map[2]
    assert pnpx api-extractor run --local --verbose
    assert pnpx api-documenter markdown -i build/api -o build/docs_temp

    for file in (find build/docs_temp -type f -name "$placeholder.*")
        set -l destination (string replace -af $placeholder \
            (escape_path $map[1]) $file |
            string replace '_temp' '')

        cat $file |
            string replace -a /$placeholder /(escape_path $map[1]) |
            string replace -a (string replace -a '_' '\_' $placeholder) $map[1] |
            string replace -a $placeholder $map[1] >$destination
    end

    echo \| '['$map[1]'](./'(escape_path $map[1])'.md)' \| >>build/docs/index.md
end

set ed_commands_file (assert mktemp)

# This will move the first h2 to the top and then replace it with a h1. This
# will fix the title generated by the api-documenter.
echo '/^## /m0
s/^## /# /
wq
' >$ed_commands_file

# Apply that command to every file
for filename in (assert find build/docs -name '*.md' -and -not -name index.md)
    assert groupcmd ed $filename <$ed_commands_file
end

# Clean up the files
assert rm -rf build/api build/docs_temp
assert rm -f $ed_commands_file
